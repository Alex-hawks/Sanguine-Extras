buildscript {
    repositories {
        mavenCentral()
        maven { url "http://files.minecraftforge.net/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:0.10.1'
    }
}

plugins {
	id "net.minecraftforge.gradle.forge" version "2.0.2"
    id 'com.matthewprenger.cursegradle' version '1.0.5'
	id 'net.franz-becker.gradle-lombok' version '1.5'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'scala'
apply plugin: 'maven-publish'

compileScala {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}


def build_number = 'CUSTOM'

if (System.getenv('BUILD_NUMBER') != null)
    build_number = System.getenv('BUILD_NUMBER')

group = package_group
archivesBaseName = mod_name
version = "${mc_version}-${mod_version}-${build_number}"

if (project.hasProperty('gitpush'))
    version += "-ss"


import net.franz_becker.gradle.lombok.task.DelombokTask

//ext.repo = Grgit.open(project.file('.'))
def gitHash = 'unknown'

//if (new File(projectDir, '.git').exists())
//    gitHash = repo.log().find().abbreviatedId

repositories {
    mavenCentral()
	// A bunch of these are for Transitive Dependencies
    maven { url "http://files.minecraftforge.net/maven" }
    maven { url "http://dvs1.progwml6.com/files/maven" }
    maven { url "http://maven.amadornes.com/" }
    maven { url "http://tehnut.info/maven/" }
    maven { url "http://maven.ic2.player.to/" }
    maven { url "http://maven.tterrag.com/" }
    maven { url "http://maven.k-4u.nl" }
    maven { url "http://maven.covers1624.net" }
    maven { url "http://maven.thiakil.com" }
    maven { url "https://www.curseforge.com/api/maven/" }
    maven { url "http://maven.blamejared.com/"}



//    ivy {
//        name "Thaumcraft"
//        artifactPattern "https://dl.dropboxusercontent.com/u/47135879/[module]-[revision]-deobf.[ext]"
//    }
}

sourceSets {
    api  {
    }
}



dependencies {
	compile 'org.scala-lang:scala-library:2.11.1'
	compile 'org.jetbrains:annotations:15.0'
	
    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"

//    compile name: "Thaumcraft", version: "${mc_version}-${thaumcraft_version}", ext: "jar"

    deobfCompile "vazkii.botania:Botania:r1.10-353"
    deobfCompile "com.azanor.baubles:Baubles:1.12-${baubles_version}"
    deobfCompile "MCMultiPart2:MCMultiPart:${mcmp_version}"
    deobfCompile "com.wayoftime.bloodmagic:BloodMagic:${mc_version}-${bm_version}"
    deobfCompile "net.sengir.forestry:forestry_${mc_version}:${forestry_version}"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:${top_version}"
    deobfCompile "info.amerifrance.guideapi:Guide-API:${guideapi_version}"
//    deobfCompile "info.tehnut.buttons:Buttons:${mc_version}-${buttons_version}:api"
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    replace "@VERSION@", project.version
    replaceIn "Constants.java"


    if (project.hasProperty('mappings_version'))
        mappings = project.mappings_version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

lombok {
    version = "1.16.6"
    sha256 = "e0a471be03e1e6b02bf019480cec7a3ac9801702bf7bf62f15d077ad4df8dd5d"
}

task delombok(type: DelombokTask) {
    args("src/main/scala", "-d", "build/sources/delomboked/scala")
}

tasks.eclipse.dependsOn installLombok

jar {
    classifier = ''
    from sourceSets.main.output
    from sourceSets.api.output
    manifest.mainAttributes(
        "Built-By": System.getProperty('user.name'),
        "Created-By": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
        "Implementation-Title": project.name,
        "Implementation-Version": project.version,
        "Built-On": "${mc_version}-${forge_version}"
    )
}

// API jar
task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
	include 'io/github/alex_hawks/SanguineExtras/api/**/*'
    classifier = 'api'
}

// Javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

javadoc {
    source = projectDir.path + "/build/sources/delomboked/java"
}

tasks.javadoc.dependsOn delombok
tasks.build.dependsOn javadoc, javadocJar, apiJar
tasks.compileScala.dependsOn compileApiScala

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact javadocJar
            artifact sourceJar
            artifact apiJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}

String getChangelogText() {
    def changelogFile = new File('changelog.txt')
    String str = ''
    String separator = '---'
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (lineCount < 3) {
            lineCount++
            if (it.startsWith(separator)) {return}
        }
        if (!it.startsWith(separator)) {
            str += "$it" + (lineCount < 3 ? ':\n\n' : '\n')
            return
        }
        done = true // once we go past the first version block, parse no more
    }
    return str
}

def curseRelations = {
//    optionalLibrary 'just-enough-items-jei'
//    optionalLibrary 'waila'
	requiredLibrary 'blood-magic'
    optionalLibrary 'botania'
}

curseforge {
    apiKey = curseToken

    project {
        id = "${curse_id}"
        changelog = getChangelogText()
        releaseType = 'beta'

        addGameVersion '1.10.2'

        relations curseRelations

        addArtifact javadocJar
        addArtifact sourceJar
        addArtifact apiJar
    }
}